plugins {
    id 'java'
    id 'org.springframework.boot' version '3.2.3'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'com.yatakeke'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

sourceSets {
    e2eTest
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    runtimeOnly 'org.postgresql:postgresql'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "com.google.truth:truth:1.4.2"
    e2eTestImplementation 'com.microsoft.playwright:playwright:1.42.0'
    e2eTestImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
}

tasks.named('test', Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "failed"
    }
}


tasks.register('e2eTest', Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.e2eTest.output.classesDirs
    classpath = sourceSets.e2eTest.runtimeClasspath
    testLogging {
        events "passed", "failed"
        showStackTraces true
        exceptionFormat = 'full'
    }
}
